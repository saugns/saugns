/*
 * IXA synthesis modulates the phase of a triangle wave with a sine wave,
 * but the triangle wave also has a non-linear phase counter, changing it
 * into a sine wave shape when the modulator amplitude is zero. Actually,
 * this triangle-to-sine morph can also be understood as phase modulation
 * and extra modulators can be combined to produce the curve it needs. To
 * get this result, a sine, a triangle, and squares can be used together.
 */
Otri f100 p+[
	// Create the triangle-to-sine wave shape phase modulation input.
	Osin a(-1/2),(1/2)~[Osqr p(1/4)]
	Otri a(1/2),(-1/2)~[Osqr p(1/4)]
	// The normal modulator, the amplitude of which is the modulation
	// index, goes here... An amplitude of 1 correponds to I(t)=pi in
	// the explanation and graph from Casio's 1992 US patent 5164530.
	Osin r1 a0 a{g5 t3}
] t3

| \1

/*
 * For comparison, plain PM done with a somewhat higher modulation index.
 */
Osin f100 p+[
	Osin r1 a0 a{g(5*(5/4)) t3}
] t3

#Q
   For more information see <https://nathan.ho.name/posts/ixa-synthesis/>
   which inspired this script, and contains samples this script imitates.
